Import("env")
import os

env["ENV"]["PKG_CONFIG_PATH"] = os.getenv(
    "PKG_CONFIG_PATH", "/usr/local/lib/pkgconfig")

env.MergeFlags(f"!pkg-config gtk+-3.0 --cflags --libs")
env.MergeFlags(f"!pkg-config asynctls --static --cflags --libs")
env.MergeFlags(f"!pkg-config nwutil --static --cflags --libs")

env.Command(
    ["i18n.c", "i18n.h"],
    ["i18n.json", "embed.py"],
    """${SOURCES[1]} i18n_json I18N_JSON_LEN $TARGETS <${SOURCES[0]}""")

env.Command(
    "icon.dat",
    "#etc/icon.png",
    """gdk-pixbuf-csource --raw --name=lip_icon $SOURCE >$TARGET""")

env.Program(
    "lip",
    ["lip.c", "ind.c", "rpl.c", "util.c", "intl.c", "i18n.c", "url.c"],
    CCFLAGS="-g -Wall -Werror",
    CPPDEFINES=["PREFIX=$PREFIX"])
